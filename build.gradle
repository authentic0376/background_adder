plugins {
    id 'java'
    id 'application'
    id 'org.javamodularity.moduleplugin' version '1.8.12'
    id 'org.openjfx.javafxplugin' version '0.0.13'
    id 'org.beryx.jlink' version '2.25.0'
}

group 'com.sprain6628'
version '1.0.0-SNAPSHOT'

repositories {
    mavenCentral()
}

ext {
    junitVersion = '5.10.0'
}

sourceCompatibility = '17'
targetCompatibility = '17'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

application {
    mainModule = 'com.sprain6628.background_adder'
    mainClass = 'com.sprain6628.background_adder.BackgroundAdderApp'
    applicationName = project.name
}

javafx {
    version = "21"
    modules = ['javafx.controls', 'javafx.swing']
}

dependencies {
    implementation("com.google.inject:guice:7.0.0")
    implementation("org.apache.xmlgraphics:batik-transcoder:1.16") {
        exclude group: "xml-apis", module: "xml-apis"
    }
    implementation("org.apache.xmlgraphics:batik-codec:1.16") {
        exclude group: "xml-apis", module: "xml-apis"
    }

    testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")
}

test {
    useJUnitPlatform()
}

jlink {
    imageZip = project.file("${buildDir}/distributions/${project.name}-${javafx.platform.classifier}.zip")
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = project.name

    }
}

jlinkZip {
    group = 'distribution'
}
